join-2-small-large [JOIN of a small and a large predicate]	PREFIX owl: <http://www.w3.org/2002/07/owl#> PREFIX dbo: <http://dbpedia.org/ontology/> SELECT (COUNT(*) AS ?count) WHERE { ?s dbo:fuelSystem ?o1 . ?s owl:sameAs ?o2 }
join-2-large-small [JOIN of a large and a small predicate]	PREFIX owl: <http://www.w3.org/2002/07/owl#> PREFIX dbo: <http://dbpedia.org/ontology/> SELECT (COUNT(*) AS ?count) WHERE { ?s owl:sameAs ?o1 . ?s dbo:fuelSystem ?o2 }
join-2-large-large [JOIN of two large predicates]	PREFIX owl: <http://www.w3.org/2002/07/owl#> PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> SELECT (COUNT(*) AS ?count) WHERE { ?s owl:sameAs ?o1 . ?s rdf:type ?o2 }
join-2-largest-result [JOIN of two predicates with the largest possible result]	PREFIX owl: <http://www.w3.org/2002/07/owl#> PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> SELECT (COUNT(*) AS ?count) WHERE { ?s owl:sameAs ?o1 . ?s rdfs:label ?o2 }
join-2-large-large-with-large-result [JOIN of two large predicates with a reasonably large result]	PREFIX dc: <http://purl.org/dc/terms/> PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> SELECT (COUNT(*) AS ?count) WHERE { ?s dc:subject ?o1 . ?s rdfs:label ?o2 }
join-2-large-large-with-small-result [JOIN of two large predicates with a small result]	PREFIX dbo: <http://dbpedia.org/ontology/> PREFIX foaf: <http://xmlns.com/foaf/0.1/> SELECT (COUNT(*) AS ?count) WHERE { ?s dbo:wikiPageRedirects ?o1 . ?s foaf:isPrimaryTopicOf ?o2 }
join-3-star-largest-sum-of-join-sizes [JOIN star of three large predicates with the largest sum of join sizes]	PREFIX owl: <http://www.w3.org/2002/07/owl#> PREFIX dbo: <http://dbpedia.org/ontology/> SELECT (COUNT(*) AS ?count) WHERE { ?s dbo:wikiPageRevisionID ?o1 . ?s owl:sameAs ?o2 . ?s dbo:wikiPageID ?o3 . }
join-3-chain-largest-sum-of-join-sizes [JOIN chain of three large predicates with the largest sum of join sizes]	PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> PREFIX dbo: <http://dbpedia.org/ontology/> SELECT (COUNT(*) AS ?count) WHERE { ?a dbo:wikiPageRedirects ?b . ?b rdf:type ?c . ?c rdfs:label ?d . }
join-xlarge-star-on-small-predicates [JOIN star of many small predicates]	PREFIX dbp: <http://dbpedia.org/property/> PREFIX dbo: <http://dbpedia.org/ontology/> SELECT (COUNT(*) AS ?count) WHERE { ?s dbo:fuelSystem ?o0 . ?s dbp:viaf ?o1 . ?s dbp:bronzeVie ?o2 . ?s dbp:assetsYear ?o3 . ?s dbp:originalLanguage ?o4 . ?s dbp:teamlink ?o5 . ?s dbp:libraryName ?o6 . ?s dbp:goldVie ?o7 . ?s dbp:3rdSeed ?o8 . ?s dbp:flagsize ?o9 . ?s dbp:creation ?o10 . ?s dbp:firstrundate ?o11 . ?s dbp:sporeprintcolor ?o12 . ?s dbo:rotationPeriod ?o13 . ?s dbp:c2a ?o14 . ?s dbp:eng1Note ?o15 . ?s dbp:keypeople ?o16 . ?s dbp:worldsteams ?o17 . ?s dbp:write ?o18 . ?s dbo:constructionMaterial ?o19 . ?s dbp:chiefscout ?o20 . ?s dbp:hub ?o21 . ?s dbp:elections ?o22 . ?s dbp:grandfathers ?o23 . ?s dbp:manofmatch ?o24 . }
join-xlarge-chain-on-small-predicates [JOIN chain of many small predicates]	PREFIX dbp: <http://dbpedia.org/property/> PREFIX dbo: <http://dbpedia.org/ontology/> SELECT (COUNT(*) AS ?count) WHERE { ?v0 dbo:fuelSystem ?v1 . ?v1 dbp:viaf ?v2 . ?v2 dbp:bronzeVie ?v3 . ?v3 dbp:assetsYear ?v4 . ?v4 dbp:originalLanguage ?v5 . ?v5 dbp:teamlink ?v6 . ?v6 dbp:libraryName ?v7 . ?v7 dbp:goldVie ?v8 . ?v8 dbp:3rdSeed ?v9 . ?v9 dbp:flagsize ?v10 . ?v10 dbp:creation ?v11 . ?v11 dbp:firstrundate ?v12 . ?v12 dbp:sporeprintcolor ?v13 . ?v13 dbo:rotationPeriod ?v14 . ?v14 dbp:c2a ?v15 . ?v15 dbp:eng1Note ?v16 . ?v16 dbp:keypeople ?v17 . ?v17 dbp:worldsteams ?v18 . ?v18 dbp:write ?v19 . ?v19 dbo:constructionMaterial ?v20 . ?v20 dbp:chiefscout ?v21 . ?v21 dbp:hub ?v22 . ?v22 dbp:elections ?v23 . ?v23 dbp:grandfathers ?v24 . ?v24 dbp:manofmatch ?v25 . }
optional-join-small-large [OPTIONAL JOIN of a small and a large predicate]	PREFIX owl: <http://www.w3.org/2002/07/owl#> PREFIX dbo: <http://dbpedia.org/ontology/> SELECT (COUNT(*) AS ?count) WHERE { ?s dbo:fuelSystem ?o1 OPTIONAL { ?s owl:sameAs ?o2 } }
optional-join-large-small [OPTIONAL JOIN of a large and a small predicate]	PREFIX owl: <http://www.w3.org/2002/07/owl#> PREFIX dbo: <http://dbpedia.org/ontology/> SELECT (COUNT(*) AS ?count) WHERE { ?s owl:sameAs ?o1 OPTIONAL { ?s dbo:fuelSystem ?o2 } }
optional-join-large-large [OPTIONAL JOIN of two large predicates]	PREFIX owl: <http://www.w3.org/2002/07/owl#> PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> SELECT (COUNT(*) AS ?count) WHERE { ?s owl:sameAs ?o1 OPTIONAL { ?s rdf:type ?o2 } }
optional-join-2-large-large-with-large-result [OPTIONAL JOIN of two large predicates with a reasonably large join result]	PREFIX dc: <http://purl.org/dc/terms/> PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> SELECT (COUNT(*) AS ?count) WHERE { ?s dc:subject ?o1 . OPTIONAL { ?s rdfs:label ?o2 } }
optional-join-2-large-large-with-small-join-result-1 [OPTIONAL JOIN of two large predicates with a small join result]	PREFIX dbo: <http://dbpedia.org/ontology/> PREFIX foaf: <http://xmlns.com/foaf/0.1/> SELECT (COUNT(*) AS ?count) WHERE { ?s dbo:wikiPageRedirects ?o1 . OPTIONAL { ?s foaf:isPrimaryTopicOf ?o2 } }
optional-join-2-large-large-with-small-join-result-2 [OPTIONAL JOIN of two large predicates with a small join result]	PREFIX dbo: <http://dbpedia.org/ontology/> PREFIX foaf: <http://xmlns.com/foaf/0.1/> SELECT (COUNT(*) AS ?count) WHERE { ?s foaf:isPrimaryTopicOf ?o1 . OPTIONAL { ?s dbo:wikiPageRedirects ?o2 } }
optional-join-3-star-1 [OPTIONAL JOIN star of three large predicates with the largest sum of join sizes]	PREFIX owl: <http://www.w3.org/2002/07/owl#> PREFIX dbo: <http://dbpedia.org/ontology/> SELECT (COUNT(*) AS ?count) WHERE { ?s dbo:wikiPageRevisionID ?o1 . ?s owl:sameAs ?o2 . OPTIONAL { ?s dbo:wikiPageID ?o3 . } }
optional-join-3-star-2 [OPTIONAL JOIN star of three large predicates with the largest sum of join sizes]	PREFIX owl: <http://www.w3.org/2002/07/owl#> PREFIX dbo: <http://dbpedia.org/ontology/> SELECT (COUNT(*) AS ?count) WHERE { ?s dbo:wikiPageRevisionID ?o1 . OPTIONAL { ?s owl:sameAs ?o2 . ?s dbo:wikiPageID ?o3 . } }
optional-join-3-chain-1 [OPTIONAL JOIN chain of three large predicates with the largest sum of join sizes]	PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> PREFIX dbo: <http://dbpedia.org/ontology/> SELECT (COUNT(*) AS ?count) WHERE { ?a dbo:wikiPageRedirects ?b . ?b rdf:type ?c . OPTIONAL { ?c rdfs:label ?d . } }
optional-join-3-chain-2 [OPTIONAL JOIN chain of three large predicates with the largest sum of join sizes]	PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> PREFIX dbo: <http://dbpedia.org/ontology/> SELECT (COUNT(*) AS ?count) WHERE { ?a dbo:wikiPageRedirects ?b . OPTIONAL { ?b rdf:type ?c . ?c rdfs:label ?d . } }
minus-join-small-large [MINUS JOIN of a small and a large predicate]	PREFIX owl: <http://www.w3.org/2002/07/owl#> PREFIX dbo: <http://dbpedia.org/ontology/> SELECT (COUNT(*) AS ?count) WHERE { ?s dbo:fuelSystem ?o1 MINUS { ?s owl:sameAs ?o2 } }
minus-join-large-small [MINUS JOIN of a large and a small predicate]	PREFIX owl: <http://www.w3.org/2002/07/owl#> PREFIX dbo: <http://dbpedia.org/ontology/> SELECT (COUNT(*) AS ?count) WHERE { ?s owl:sameAs ?o1 MINUS { ?s dbo:fuelSystem ?o2 } }
minus-join-large-large [MINUS JOIN of two large predicates]	PREFIX owl: <http://www.w3.org/2002/07/owl#> PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> SELECT (COUNT(*) AS ?count) WHERE { ?s owl:sameAs ?o1 MINUS { ?s rdf:type ?o2 } }
minus-join-2-large-large-with-large-result [MINUS JOIN of two large predicates with a reasonably large join result]	PREFIX dc: <http://purl.org/dc/terms/> PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> SELECT (COUNT(*) AS ?count) WHERE { ?s dc:subject ?o1 . MINUS { ?s rdfs:label ?o2 } }
minus-join-2-large-large-with-small-join-result-1 [MINUS JOIN of two large predicates with a small join result]	PREFIX dbo: <http://dbpedia.org/ontology/> PREFIX foaf: <http://xmlns.com/foaf/0.1/> SELECT (COUNT(*) AS ?count) WHERE { ?s dbo:wikiPageRedirects ?o1 . MINUS { ?s foaf:isPrimaryTopicOf ?o2 } }
minus-join-2-large-large-with-small-join-result-2 [MINUS JOIN of two large predicates with a small join result]	PREFIX dbo: <http://dbpedia.org/ontology/> PREFIX foaf: <http://xmlns.com/foaf/0.1/> SELECT (COUNT(*) AS ?count) WHERE { ?s foaf:isPrimaryTopicOf ?o1 . MINUS { ?s dbo:wikiPageRedirects ?o2 } }
minus-join-3-star-1 [MINUS JOIN star of three large predicates with the largest sum of join sizes]	PREFIX owl: <http://www.w3.org/2002/07/owl#> PREFIX dbo: <http://dbpedia.org/ontology/> SELECT (COUNT(*) AS ?count) WHERE { ?s dbo:wikiPageRevisionID ?o1 . ?s owl:sameAs ?o2 . MINUS { ?s dbo:wikiPageID ?o3 . } }
minus-join-3-star-2 [MINUS JOIN star of three large predicates with the largest sum of join sizes]	PREFIX owl: <http://www.w3.org/2002/07/owl#> PREFIX dbo: <http://dbpedia.org/ontology/> SELECT (COUNT(*) AS ?count) WHERE { ?s dbo:wikiPageRevisionID ?o1 . MINUS { ?s owl:sameAs ?o2 . ?s dbo:wikiPageID ?o3 . } }
minus-join-3-chain-1 [MINUS JOIN chain of three large predicates with the largest sum of join sizes]	PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> PREFIX dbo: <http://dbpedia.org/ontology/> SELECT (COUNT(*) AS ?count) WHERE { ?a dbo:wikiPageRedirects ?b . ?b rdf:type ?c . MINUS { ?c rdfs:label ?d . } }
minus-join-3-chain-2 [MINUS JOIN chain of three large predicates with the largest sum of join sizes]	PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> PREFIX dbo: <http://dbpedia.org/ontology/> SELECT (COUNT(*) AS ?count) WHERE { ?a dbo:wikiPageRedirects ?b . MINUS { ?b rdf:type ?c . ?c rdfs:label ?d . } }
exists-join-small-large [EXISTS JOIN of a small and a large predicate]	PREFIX owl: <http://www.w3.org/2002/07/owl#> PREFIX dbo: <http://dbpedia.org/ontology/> SELECT (COUNT(*) AS ?count) WHERE { ?s dbo:fuelSystem ?o1 FILTER EXISTS { ?s owl:sameAs ?o2 } }
exists-join-large-small [EXISTS JOIN of a large and a small predicate]	PREFIX owl: <http://www.w3.org/2002/07/owl#> PREFIX dbo: <http://dbpedia.org/ontology/> SELECT (COUNT(*) AS ?count) WHERE { ?s owl:sameAs ?o1 FILTER EXISTS { ?s dbo:fuelSystem ?o2 } }
exists-join-large-large [EXISTS JOIN of two large predicates]	PREFIX owl: <http://www.w3.org/2002/07/owl#> PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> SELECT (COUNT(*) AS ?count) WHERE { ?s owl:sameAs ?o1 FILTER EXISTS { ?s rdf:type ?o2 } }
exists-join-2-large-large-with-large-result [EXISTS JOIN of two large predicates with a reasonably large join result]	PREFIX dc: <http://purl.org/dc/terms/> PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> SELECT (COUNT(*) AS ?count) WHERE { ?s dc:subject ?o1 . FILTER EXISTS { ?s rdfs:label ?o2 } }
exists-join-2-large-large-with-small-join-result-1 [EXISTS JOIN of two large predicates with a small join result]	PREFIX dbo: <http://dbpedia.org/ontology/> PREFIX foaf: <http://xmlns.com/foaf/0.1/> SELECT (COUNT(*) AS ?count) WHERE { ?s dbo:wikiPageRedirects ?o1 . FILTER EXISTS { ?s foaf:isPrimaryTopicOf ?o2 } }
exists-join-2-large-large-with-small-join-result-2 [EXISTS JOIN of two large predicates with a small join result]	PREFIX dbo: <http://dbpedia.org/ontology/> PREFIX foaf: <http://xmlns.com/foaf/0.1/> SELECT (COUNT(*) AS ?count) WHERE { ?s foaf:isPrimaryTopicOf ?o1 . FILTER EXISTS { ?s dbo:wikiPageRedirects ?o2 } }
exists-join-3-star-1 [EXISTS JOIN star of three large predicates with the largest sum of join sizes]	PREFIX owl: <http://www.w3.org/2002/07/owl#> PREFIX dbo: <http://dbpedia.org/ontology/> SELECT (COUNT(*) AS ?count) WHERE { ?s dbo:wikiPageRevisionID ?o1 . ?s owl:sameAs ?o2 . FILTER EXISTS { ?s dbo:wikiPageID ?o3 . } }
exists-join-3-star-2 [EXISTS JOIN star of three large predicates with the largest sum of join sizes]	PREFIX owl: <http://www.w3.org/2002/07/owl#> PREFIX dbo: <http://dbpedia.org/ontology/> SELECT (COUNT(*) AS ?count) WHERE { ?s dbo:wikiPageRevisionID ?o1 . FILTER EXISTS { ?s owl:sameAs ?o2 . ?s dbo:wikiPageID ?o3 . } }
exists-join-3-chain-1 [EXISTS JOIN chain of three large predicates with the largest sum of join sizes]	PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> PREFIX dbo: <http://dbpedia.org/ontology/> SELECT (COUNT(*) AS ?count) WHERE { ?a dbo:wikiPageRedirects ?b . ?b rdf:type ?c . FILTER EXISTS { ?c rdfs:label ?d . } }
exists-join-3-chain-2 [EXISTS JOIN chain of three large predicates with the largest sum of join sizes]	PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> PREFIX dbo: <http://dbpedia.org/ontology/> SELECT (COUNT(*) AS ?count) WHERE { ?a dbo:wikiPageRedirects ?b . FILTER EXISTS { ?b rdf:type ?c . ?c rdfs:label ?d . } }
union-no-constraint [UNION of two large predicates, no constraint]	PREFIX owl: <http://www.w3.org/2002/07/owl#> PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> SELECT (COUNT(*) AS ?count) WHERE { { ?s owl:sameAs ?o } UNION { ?s rdf:type ?o } }
union-constraint-from-star [UNION of two large predicates, constrained by a large predicate known to have join partners]	PREFIX owl: <http://www.w3.org/2002/07/owl#> PREFIX dbo: <http://dbpedia.org/ontology/> SELECT (COUNT(*) AS ?count) WHERE { { ?s dbo:wikiPageRevisionID ?o1 } UNION { ?s owl:sameAs ?o1 } ?s dbo:wikiPageID ?o2 }
union-constraint-small-join [UNION of two large predicates, constrained by a join with a small result]	PREFIX dbo: <http://dbpedia.org/ontology/> PREFIX foaf: <http://xmlns.com/foaf/0.1/> SELECT (COUNT(*) AS ?count) WHERE { { ?s dbo:wikiPageRedirects ?o1 } UNION { ?s dbo:wikiPageRevisionID ?o1 } ?s foaf:isPrimaryTopicOf ?o2 }
union-constraint-large-join [UNION of two large predicates, constrained by a join with a large result]	PREFIX dc: <http://purl.org/dc/terms/> PREFIX dbo: <http://dbpedia.org/ontology/> PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> SELECT (COUNT(*) AS ?count) WHERE { { ?s rdfs:label ?o1 } UNION { ?s dbo:abstract ?o1 } ?s dc:subject ?o2 }
union-constraint-filter-restrictive [UNION of two large predicates, constraint by restrictive FILTER]	PREFIX owl: <http://www.w3.org/2002/07/owl#> PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> SELECT (COUNT(*) AS ?count) WHERE { { ?s owl:sameAs ?o } UNION { ?s rdf:type ?o } FILTER (?s = ?o) }
multicolumn-join-small [Multicolumn JOIN small]	PREFIX dbp: <http://dbpedia.org/property/> SELECT (COUNT(*) AS ?count) WHERE { ?s dbp:goals ?o . ?s dbp:score ?o }
multicolumn-join-large [Multicolumn JOIN large]	PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> PREFIX dbo: <http://dbpedia.org/ontology/> SELECT (COUNT(*) AS ?count) WHERE { ?s dbo:abstract ?o . ?s rdfs:comment ?o }
group-by-count-object-high-multiplicity [GROUP BY with COUNT, for object with high multiplicity]	SELECT ?object (COUNT(?subject) AS ?count) { ?subject <http://purl.org/dc/elements/1.1/language> ?object . } GROUP BY ?object ORDER BY DESC(?count) LIMIT 10
group-by-count-object-low-multiplicity [GROUP BY with COUNT, for object with low multiplicity]	PREFIX owl: <http://www.w3.org/2002/07/owl#> SELECT ?object (COUNT(?subject) AS ?count) { ?subject owl:sameAs ?object . } GROUP BY ?object ORDER BY DESC(?count) LIMIT 10
group-by-count-object-wrong-sort-order [GROUP BY with COUNT, for object but not sorted by object]	PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> SELECT ?o1 (COUNT(?s) AS ?count) { ?s <http://purl.org/dc/elements/1.1/language> ?o1 . ?s rdf:type ?o2 . } GROUP BY ?o1 ORDER BY DESC(?count) LIMIT 10
group-by-complex-aggregate [GROUP BY with COUNT and MIN and MAX and SAMPLE]	PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> SELECT ?o1 (COUNT(?s) AS ?count) (MIN(?s) AS ?min) (MAX(?s) AS ?max) (SAMPLE(?s) AS ?sample) { ?s <http://purl.org/dc/elements/1.1/language> ?o1 . ?s rdf:type ?o2 . } GROUP BY ?o1 ORDER BY DESC(?count) LIMIT 10
group-by-implicit-numeric-baseline [Implicit GROUP BY with COUNT on numeric predicate as baseline]	PREFIX dbo: <http://dbpedia.org/ontology/> SELECT (COUNT(?o) AS ?count) { ?s dbo:absoluteMagnitude ?o. }
group-by-implicit-numeric-sum [Implicit GROUP BY with SUM on numeric predicate]	PREFIX dbo: <http://dbpedia.org/ontology/> SELECT (SUM(?o) AS ?sum) { ?s dbo:absoluteMagnitude ?o. }
group-by-implicit-numeric-min [Implicit GROUP BY with MIN on numeric predicate]	PREFIX dbo: <http://dbpedia.org/ontology/> SELECT (MIN(?o) AS ?min) { ?s dbo:absoluteMagnitude ?o. }
group-by-implicit-numeric-max [Implicit GROUP BY with MAX on numeric predicate]	PREFIX dbo: <http://dbpedia.org/ontology/> SELECT (MAX(?o) AS ?max) { ?s dbo:absoluteMagnitude ?o. }
group-by-implicit-numeric-avg [Implicit GROUP BY with AVG on numeric predicate]	PREFIX dbo: <http://dbpedia.org/ontology/> SELECT (AVG(?o) AS ?avg) { ?s dbo:absoluteMagnitude ?o. }
group-by-implicit-string-baseline [Implicit GROUP BY with COUNT on string predicate as baseline]	PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> SELECT (COUNT(?o) AS ?count) { ?s rdfs:label ?o. }
group-by-implicit-string-min [Implicit GROUP BY with MIN on string predicate]	PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> SELECT (MIN(?o) AS ?min) { ?s rdfs:label ?o. }
group-by-implicit-string-max [Implicit GROUP BY with MAX on string predicate]	PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> SELECT (MAX(?o) AS ?max) { ?s rdfs:label ?o. }
group-by-string-groupconcat [GROUP BY with GROUP_CONCAT on string predicate with high subject multiplicity]	PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> SELECT (SUM(STRLEN(?cat)) AS ?sum) { { SELECT (GROUP_CONCAT(?o; SEPARATOR=" ") AS ?cat) { ?s rdfs:label ?o. } GROUP BY ?s } }
distinct-count-object-high-multiplicity [COUNT DISTINCT, for object with high multiplicity]	SELECT (COUNT(DISTINCT ?object) AS ?count) { ?subject <http://purl.org/dc/elements/1.1/language> ?object . }
distinct-count-object-low-multiplicity [COUNT DISTINCT, for object with low multiplicity]	PREFIX owl: <http://www.w3.org/2002/07/owl#> SELECT (COUNT(DISTINCT ?object) AS ?count) { ?subject owl:sameAs ?object . }
distinct-count-object-wrong-sort-order [COUNT DISTINCT, for object but not sorted by object]	PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> SELECT (COUNT(DISTINCT ?o1) AS ?count) { ?s <http://purl.org/dc/elements/1.1/language> ?o1 . ?s rdf:type ?o2 . }
transitive-path-plus [Transitive path with plus]	PREFIX dbp: <http://dbpedia.org/property/> SELECT (COUNT(*) AS ?count) { ?s dbp:neighboringMunicipalities+ ?o }
transitive-path-plus-fixed-subject [Transitive path with plus and a fixed subject]	PREFIX dbp: <http://dbpedia.org/property/> PREFIX dbr: <http://dbpedia.org/resource/> SELECT (COUNT(*) AS ?count) { dbr:Besazio dbp:neighboringMunicipalities+ ?o }
transitive-path-large-join-and-plus [Transitive path with a large join and plus]	PREFIX dbp: <http://dbpedia.org/property/> PREFIX dbo: <http://dbpedia.org/ontology/> SELECT (COUNT(*) AS ?count) { ?s dbo:neighboringMunicipality/dbp:neighboringMunicipalities+ ?o }
transitive-path-small-join-and-plus [Transitive path with a small join and plus]	PREFIX dbp: <http://dbpedia.org/property/> PREFIX dbo: <http://dbpedia.org/ontology/> SELECT (COUNT(*) AS ?count) { ?s dbo:hometown/dbp:neighboringMunicipalities+ ?o }
regex-3-contains [CONTAINS filter with fixed string of length 3]	PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> SELECT (COUNT(*) AS ?count) WHERE { ?s rdfs:label ?o FILTER CONTAINS(?o, "com") }
regex-3-fixed [REGEX filter with fixed string of length 3]	PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> SELECT (COUNT(*) AS ?count) WHERE { ?s rdfs:label ?o FILTER REGEX(?o, "com") }
regex-3 [REGEX filter with expression of length 3]	PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> SELECT (COUNT(*) AS ?count) WHERE { ?s rdfs:label ?o FILTER REGEX(?o, "c.m") }
regex-prefix-1 [REGEX filter with prefix of length 1]	PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> SELECT (COUNT(*) AS ?count) WHERE { ?s rdfs:label ?o FILTER REGEX(?o, "^C") }
regex-prefix-2 [REGEX filter with prefix of length 2]	PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> SELECT (COUNT(*) AS ?count) WHERE { ?s rdfs:label ?object FILTER REGEX(?object, "^Co") }
regex-prefix-3 [REGEX filter with prefix of length 3]	PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> SELECT (COUNT(*) AS ?count) WHERE { ?s rdfs:label ?object FILTER REGEX(?object, "^Com") }
strlen [String length for large string predicate]	PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> SELECT (SUM(STRLEN(?o)) AS ?checksum) { ?s rdfs:label ?o . }
strbefore [STRBEFORE string function]	PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> SELECT (SUM(STRLEN(STRBEFORE(?o, "a"))) AS ?checksum) { ?s rdfs:label ?o . }
strafter [STRAFTER string function]	PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> SELECT (SUM(STRLEN(STRAFTER(?o, "a"))) AS ?checksum) { ?s rdfs:label ?o . }
strstarts [STRSTARTS string function]	PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> PREFIX xsd: <http://www.w3.org/2001/XMLSchema#> SELECT (SUM(xsd:integer(STRSTARTS(?o, "a"))) AS ?count) { ?s rdfs:label ?o . }
strends [STRENDS string function]	PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> PREFIX xsd: <http://www.w3.org/2001/XMLSchema#> SELECT (SUM(xsd:integer(STRENDS(?o, "a"))) AS ?count) { ?s rdfs:label ?o . }
result-size-tiny [Export 10 tuples]	PREFIX owl: <http://www.w3.org/2002/07/owl#> SELECT ?s ?o WHERE { ?s owl:sameAs ?o } LIMIT 10
result-size-small [Export 1000 tuples]	PREFIX owl: <http://www.w3.org/2002/07/owl#> SELECT ?s ?o WHERE { ?s owl:sameAs ?o } LIMIT 1000
result-size-medium [Export 100K tuples]	PREFIX owl: <http://www.w3.org/2002/07/owl#> SELECT ?s ?o WHERE { ?s owl:sameAs ?o } LIMIT 100000
result-size-large [Export 1M tuples]	PREFIX owl: <http://www.w3.org/2002/07/owl#> SELECT ?s ?o WHERE { ?s owl:sameAs ?o } LIMIT 1000000
result-size-xlarge [Export 10M tuples]	PREFIX owl: <http://www.w3.org/2002/07/owl#> SELECT ?s ?o WHERE { ?s owl:sameAs ?o } LIMIT 10000000
numeric-baseline [Baseline for numeric queries]	PREFIX dbo: <http://dbpedia.org/ontology/> SELECT (SUM(?o) AS ?sum) WHERE { ?s dbo:absoluteMagnitude ?o }
numeric-abs [ABS function]	PREFIX dbo: <http://dbpedia.org/ontology/> SELECT (SUM(ABS(?o)) AS ?sum) WHERE { ?s dbo:absoluteMagnitude ?o }
numeric-ceil [CEIL function]	PREFIX dbo: <http://dbpedia.org/ontology/> SELECT (SUM(CEIL(?o)) AS ?sum) WHERE { ?s dbo:absoluteMagnitude ?o }
numeric-floor [FLOOR function]	PREFIX dbo: <http://dbpedia.org/ontology/> SELECT (SUM(FLOOR(?o)) AS ?sum) WHERE { ?s dbo:absoluteMagnitude ?o }
numeric-round [ROUND function]	PREFIX dbo: <http://dbpedia.org/ontology/> SELECT (SUM(ROUND(?o)) AS ?sum) WHERE { ?s dbo:absoluteMagnitude ?o }
numeric-add [Addition]	PREFIX dbo: <http://dbpedia.org/ontology/> SELECT (SUM(?o + ?o) AS ?sum) WHERE { ?s dbo:absoluteMagnitude ?o }
numeric-greater [Greater than]	PREFIX dbo: <http://dbpedia.org/ontology/> SELECT (SUM(?o > 0) AS ?sum) WHERE { ?s dbo:absoluteMagnitude ?o }
numeric-filter-bin-search-fifty-fifty [Numeric FILTER that filters out 50 percent of the values]	PREFIX dbo: <http://dbpedia.org/ontology/> SELECT (COUNT(?s) AS ?count) { ?s dbo:absoluteMagnitude ?o . FILTER (?o >= 11.4) }
numeric-filter-bin-search-seventy-thirty [Numeric FILTER that filters out 70 percent of the values]	PREFIX dbo: <http://dbpedia.org/ontology/> SELECT (COUNT(?s) AS ?count) { ?s dbo:absoluteMagnitude ?o . FILTER (?o >= 12.9) }
numeric-filter-bin-search-ninetyfive-five [Numeric FILTER that filters out 95 percent of the values]	PREFIX dbo: <http://dbpedia.org/ontology/> SELECT (COUNT(?s) AS ?count) { ?s dbo:absoluteMagnitude ?o . FILTER (?o >= 20.8) }
filter-few-results [FILTER that filters out most rows and has few results]	PREFIX owl: <http://www.w3.org/2002/07/owl#> SELECT (COUNT(?s) AS ?count) { ?s owl:sameAs ?o . FILTER (?s = ?o) }
filter-many-results [FILTER that filters out few rows and has many results]	PREFIX owl: <http://www.w3.org/2002/07/owl#> SELECT (COUNT(?s) AS ?count) { ?s owl:sameAs ?o . FILTER (?s != ?o) }
filter-language-en [FILTER on string predicate for only English literals]	PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> SELECT (COUNT(?s) AS ?count) { ?s rdfs:label ?o . FILTER(LANG(?o) = "en") }
date-year [YEAR function]	PREFIX dbo: <http://dbpedia.org/ontology/> SELECT (SUM(YEAR(?o)) AS ?sum) WHERE { ?s dbo:years ?o }
date-month [MONTH function]	PREFIX dbo: <http://dbpedia.org/ontology/> SELECT (SUM(MONTH(?o)) AS ?sum) WHERE { ?s dbo:years ?o }
date-day [DAY function]	PREFIX dbo: <http://dbpedia.org/ontology/> SELECT (SUM(DAY(?o)) AS ?sum) WHERE { ?s dbo:years ?o }
number-of-triples [Total number of triples]	SELECT (COUNT(?s) AS ?count) WHERE { ?s ?p ?o }
number-of-subjects [Total number of distinct subjects]	SELECT (COUNT(DISTINCT ?s) AS ?count) WHERE { ?s ?p ?o }
number-of-predicates [Total number of distinct predicates]	SELECT (COUNT(DISTINCT ?p) AS ?count) WHERE { ?s ?p ?o }
number-of-objects [Total number of distinct objects]	SELECT (COUNT(DISTINCT ?o) AS ?count) WHERE { ?s ?p ?o }
number-of-literals [Total number of literals]	SELECT (COUNT(?o) AS ?count) WHERE { ?s ?p ?o FILTER ISLITERAL(?o) }
number-of-blank-nodes [Total number of blank nodes]	SELECT (COUNT(?s) AS ?count) WHERE { ?s ?p ?o FILTER ISBLANK(?s) }
