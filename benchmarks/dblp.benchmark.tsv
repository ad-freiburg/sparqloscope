join-2-small-large [JOIN of a small and a large predicate]	PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> PREFIX dblp: <https://dblp.org/rdf/schema#> SELECT (COUNT(*) AS ?count) WHERE { ?s dblp:formerStreamTitle ?o1 . ?s rdf:type ?o2 }
join-2-large-small [JOIN of a large and a small predicate]	PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> PREFIX dblp: <https://dblp.org/rdf/schema#> SELECT (COUNT(*) AS ?count) WHERE { ?s rdf:type ?o1 . ?s dblp:formerStreamTitle ?o2 }
join-2-large-large [JOIN of two large predicates]	PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> PREFIX dblp: <https://dblp.org/rdf/schema#> SELECT (COUNT(*) AS ?count) WHERE { ?s rdf:type ?o1 . ?s dblp:hasSignature ?o2 }
join-2-largest-result [JOIN of two predicates with the largest possible result]	PREFIX dblp: <https://dblp.org/rdf/schema#> SELECT (COUNT(*) AS ?count) WHERE { ?s dblp:hasSignature ?o1 . ?s dblp:createdBy ?o2 }
join-2-large-large-with-large-result [JOIN of two large predicates with a reasonably large result]	PREFIX dblp: <https://dblp.org/rdf/schema#> SELECT (COUNT(*) AS ?count) WHERE { ?s dblp:hasSignature ?o1 . ?s dblp:createdBy ?o2 }
join-2-large-large-with-small-result [JOIN of two large predicates with a small result]	PREFIX dblp: <https://dblp.org/rdf/schema#> SELECT (COUNT(*) AS ?count) WHERE { ?s dblp:publishedAsPartOf ?o1 . ?s dblp:publishedInJournalVolume ?o2 }
join-3-star-largest-sum-of-join-sizes [JOIN star of three large predicates with the largest sum of join sizes]	PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> PREFIX dblp: <https://dblp.org/rdf/schema#> SELECT (COUNT(*) AS ?count) WHERE { ?s dblp:signatureOrdinal ?o1 . ?s rdf:type ?o2 . ?s dblp:signatureDblpName ?o3 . }
join-3-chain-largest-sum-of-join-sizes [JOIN chain of three large predicates with the largest sum of join sizes]	PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> PREFIX dblp: <https://dblp.org/rdf/schema#> SELECT (COUNT(*) AS ?count) WHERE { ?a dblp:signaturePublication ?b . ?b rdf:type ?c . ?c rdfs:subClassOf ?d . }
join-xlarge-star-on-small-predicates [JOIN star of many small predicates]	PREFIX terms: <http://purl.org/dc/terms/> PREFIX owl: <http://www.w3.org/2002/07/owl#> PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> PREFIX dblp: <https://dblp.org/rdf/schema#> SELECT (COUNT(*) AS ?count) WHERE { ?s dblp:formerStreamTitle ?o0 . ?s dblp:awardWebpage ?o1 . ?s dblp:successorStream ?o2 . ?s dblp:predeccessorStream ?o3 . ?s rdfs:comment ?o4 . ?s rdfs:domain ?o5 . ?s rdfs:range ?o6 . ?s rdfs:subPropertyOf ?o7 . ?s owl:equivalentProperty ?o8 . ?s rdfs:subClassOf ?o9 . ?s owl:equivalentClass ?o10 . ?s owl:inverseOf ?o11 . ?s dblp:publishersAddress ?o12 . ?s dblp:publishedInBookChapter ?o13 . ?s terms:creator ?o14 . ?s terms:abstract ?o15 . ?s terms:title ?o16 . ?s owl:priorVersion ?o17 . ?s owl:versionInfo ?o18 . ?s owl:versionIRI ?o19 . ?s terms:modified ?o20 . ?s terms:license ?o21 . ?s terms:description ?o22 . }
join-xlarge-chain-on-small-predicates [JOIN chain of many small predicates]	PREFIX terms: <http://purl.org/dc/terms/> PREFIX owl: <http://www.w3.org/2002/07/owl#> PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> PREFIX dblp: <https://dblp.org/rdf/schema#> SELECT (COUNT(*) AS ?count) WHERE { ?v0 dblp:formerStreamTitle ?v1 . ?v1 dblp:awardWebpage ?v2 . ?v2 dblp:successorStream ?v3 . ?v3 dblp:predeccessorStream ?v4 . ?v4 rdfs:comment ?v5 . ?v5 rdfs:domain ?v6 . ?v6 rdfs:range ?v7 . ?v7 rdfs:subPropertyOf ?v8 . ?v8 owl:equivalentProperty ?v9 . ?v9 rdfs:subClassOf ?v10 . ?v10 owl:equivalentClass ?v11 . ?v11 owl:inverseOf ?v12 . ?v12 dblp:publishersAddress ?v13 . ?v13 dblp:publishedInBookChapter ?v14 . ?v14 terms:creator ?v15 . ?v15 terms:abstract ?v16 . ?v16 terms:title ?v17 . ?v17 owl:priorVersion ?v18 . ?v18 owl:versionInfo ?v19 . ?v19 owl:versionIRI ?v20 . ?v20 terms:modified ?v21 . ?v21 terms:license ?v22 . ?v22 terms:description ?v23 . }
optional-join-small-large [OPTIONAL JOIN of a small and a large predicate]	PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> PREFIX dblp: <https://dblp.org/rdf/schema#> SELECT (COUNT(*) AS ?count) WHERE { ?s dblp:formerStreamTitle ?o1 OPTIONAL { ?s rdf:type ?o2 } }
optional-join-large-small [OPTIONAL JOIN of a large and a small predicate]	PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> PREFIX dblp: <https://dblp.org/rdf/schema#> SELECT (COUNT(*) AS ?count) WHERE { ?s rdf:type ?o1 OPTIONAL { ?s dblp:formerStreamTitle ?o2 } }
optional-join-large-large [OPTIONAL JOIN of two large predicates]	PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> PREFIX dblp: <https://dblp.org/rdf/schema#> SELECT (COUNT(*) AS ?count) WHERE { ?s rdf:type ?o1 OPTIONAL { ?s dblp:hasSignature ?o2 } }
optional-join-2-large-large-with-large-result [OPTIONAL JOIN of two large predicates with a reasonably large join result]	PREFIX dblp: <https://dblp.org/rdf/schema#> SELECT (COUNT(*) AS ?count) WHERE { ?s dblp:hasSignature ?o1 . OPTIONAL { ?s dblp:createdBy ?o2 } }
optional-join-2-large-large-with-small-join-result-1 [OPTIONAL JOIN of two large predicates with a small join result]	PREFIX dblp: <https://dblp.org/rdf/schema#> SELECT (COUNT(*) AS ?count) WHERE { ?s dblp:publishedAsPartOf ?o1 . OPTIONAL { ?s dblp:publishedInJournalVolume ?o2 } }
optional-join-2-large-large-with-small-join-result-2 [OPTIONAL JOIN of two large predicates with a small join result]	PREFIX dblp: <https://dblp.org/rdf/schema#> SELECT (COUNT(*) AS ?count) WHERE { ?s dblp:publishedInJournalVolume ?o1 . OPTIONAL { ?s dblp:publishedAsPartOf ?o2 } }
optional-join-3-star-1 [OPTIONAL JOIN star of three large predicates with the largest sum of join sizes]	PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> PREFIX dblp: <https://dblp.org/rdf/schema#> SELECT (COUNT(*) AS ?count) WHERE { ?s dblp:signatureOrdinal ?o1 . ?s rdf:type ?o2 . OPTIONAL { ?s dblp:signatureDblpName ?o3 . } }
optional-join-3-star-2 [OPTIONAL JOIN star of three large predicates with the largest sum of join sizes]	PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> PREFIX dblp: <https://dblp.org/rdf/schema#> SELECT (COUNT(*) AS ?count) WHERE { ?s dblp:signatureOrdinal ?o1 . OPTIONAL { ?s rdf:type ?o2 . ?s dblp:signatureDblpName ?o3 . } }
optional-join-3-chain-1 [OPTIONAL JOIN chain of three large predicates with the largest sum of join sizes]	PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> PREFIX dblp: <https://dblp.org/rdf/schema#> SELECT (COUNT(*) AS ?count) WHERE { ?a dblp:signaturePublication ?b . ?b rdf:type ?c . OPTIONAL { ?c rdfs:subClassOf ?d . } }
optional-join-3-chain-2 [OPTIONAL JOIN chain of three large predicates with the largest sum of join sizes]	PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> PREFIX dblp: <https://dblp.org/rdf/schema#> SELECT (COUNT(*) AS ?count) WHERE { ?a dblp:signaturePublication ?b . OPTIONAL { ?b rdf:type ?c . ?c rdfs:subClassOf ?d . } }
minus-join-small-large [MINUS JOIN of a small and a large predicate]	PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> PREFIX dblp: <https://dblp.org/rdf/schema#> SELECT (COUNT(*) AS ?count) WHERE { ?s dblp:formerStreamTitle ?o1 MINUS { ?s rdf:type ?o2 } }
minus-join-large-small [MINUS JOIN of a large and a small predicate]	PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> PREFIX dblp: <https://dblp.org/rdf/schema#> SELECT (COUNT(*) AS ?count) WHERE { ?s rdf:type ?o1 MINUS { ?s dblp:formerStreamTitle ?o2 } }
minus-join-large-large [MINUS JOIN of two large predicates]	PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> PREFIX dblp: <https://dblp.org/rdf/schema#> SELECT (COUNT(*) AS ?count) WHERE { ?s rdf:type ?o1 MINUS { ?s dblp:hasSignature ?o2 } }
minus-join-2-large-large-with-large-result [MINUS JOIN of two large predicates with a reasonably large join result]	PREFIX dblp: <https://dblp.org/rdf/schema#> SELECT (COUNT(*) AS ?count) WHERE { ?s dblp:hasSignature ?o1 . MINUS { ?s dblp:createdBy ?o2 } }
minus-join-2-large-large-with-small-join-result-1 [MINUS JOIN of two large predicates with a small join result]	PREFIX dblp: <https://dblp.org/rdf/schema#> SELECT (COUNT(*) AS ?count) WHERE { ?s dblp:publishedAsPartOf ?o1 . MINUS { ?s dblp:publishedInJournalVolume ?o2 } }
minus-join-2-large-large-with-small-join-result-2 [MINUS JOIN of two large predicates with a small join result]	PREFIX dblp: <https://dblp.org/rdf/schema#> SELECT (COUNT(*) AS ?count) WHERE { ?s dblp:publishedInJournalVolume ?o1 . MINUS { ?s dblp:publishedAsPartOf ?o2 } }
minus-join-3-star-1 [MINUS JOIN star of three large predicates with the largest sum of join sizes]	PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> PREFIX dblp: <https://dblp.org/rdf/schema#> SELECT (COUNT(*) AS ?count) WHERE { ?s dblp:signatureOrdinal ?o1 . ?s rdf:type ?o2 . MINUS { ?s dblp:signatureDblpName ?o3 . } }
minus-join-3-star-2 [MINUS JOIN star of three large predicates with the largest sum of join sizes]	PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> PREFIX dblp: <https://dblp.org/rdf/schema#> SELECT (COUNT(*) AS ?count) WHERE { ?s dblp:signatureOrdinal ?o1 . MINUS { ?s rdf:type ?o2 . ?s dblp:signatureDblpName ?o3 . } }
minus-join-3-chain-1 [MINUS JOIN chain of three large predicates with the largest sum of join sizes]	PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> PREFIX dblp: <https://dblp.org/rdf/schema#> SELECT (COUNT(*) AS ?count) WHERE { ?a dblp:signaturePublication ?b . ?b rdf:type ?c . MINUS { ?c rdfs:subClassOf ?d . } }
minus-join-3-chain-2 [MINUS JOIN chain of three large predicates with the largest sum of join sizes]	PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> PREFIX dblp: <https://dblp.org/rdf/schema#> SELECT (COUNT(*) AS ?count) WHERE { ?a dblp:signaturePublication ?b . MINUS { ?b rdf:type ?c . ?c rdfs:subClassOf ?d . } }
exists-join-small-large [EXISTS JOIN of a small and a large predicate]	PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> PREFIX dblp: <https://dblp.org/rdf/schema#> SELECT (COUNT(*) AS ?count) WHERE { ?s dblp:formerStreamTitle ?o1 FILTER EXISTS { ?s rdf:type ?o2 } }
exists-join-large-small [EXISTS JOIN of a large and a small predicate]	PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> PREFIX dblp: <https://dblp.org/rdf/schema#> SELECT (COUNT(*) AS ?count) WHERE { ?s rdf:type ?o1 FILTER EXISTS { ?s dblp:formerStreamTitle ?o2 } }
exists-join-large-large [EXISTS JOIN of two large predicates]	PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> PREFIX dblp: <https://dblp.org/rdf/schema#> SELECT (COUNT(*) AS ?count) WHERE { ?s rdf:type ?o1 FILTER EXISTS { ?s dblp:hasSignature ?o2 } }
exists-join-2-large-large-with-large-result [EXISTS JOIN of two large predicates with a reasonably large join result]	PREFIX dblp: <https://dblp.org/rdf/schema#> SELECT (COUNT(*) AS ?count) WHERE { ?s dblp:hasSignature ?o1 . FILTER EXISTS { ?s dblp:createdBy ?o2 } }
exists-join-2-large-large-with-small-join-result-1 [EXISTS JOIN of two large predicates with a small join result]	PREFIX dblp: <https://dblp.org/rdf/schema#> SELECT (COUNT(*) AS ?count) WHERE { ?s dblp:publishedAsPartOf ?o1 . FILTER EXISTS { ?s dblp:publishedInJournalVolume ?o2 } }
exists-join-2-large-large-with-small-join-result-2 [EXISTS JOIN of two large predicates with a small join result]	PREFIX dblp: <https://dblp.org/rdf/schema#> SELECT (COUNT(*) AS ?count) WHERE { ?s dblp:publishedInJournalVolume ?o1 . FILTER EXISTS { ?s dblp:publishedAsPartOf ?o2 } }
exists-join-3-star-1 [EXISTS JOIN star of three large predicates with the largest sum of join sizes]	PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> PREFIX dblp: <https://dblp.org/rdf/schema#> SELECT (COUNT(*) AS ?count) WHERE { ?s dblp:signatureOrdinal ?o1 . ?s rdf:type ?o2 . FILTER EXISTS { ?s dblp:signatureDblpName ?o3 . } }
exists-join-3-star-2 [EXISTS JOIN star of three large predicates with the largest sum of join sizes]	PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> PREFIX dblp: <https://dblp.org/rdf/schema#> SELECT (COUNT(*) AS ?count) WHERE { ?s dblp:signatureOrdinal ?o1 . FILTER EXISTS { ?s rdf:type ?o2 . ?s dblp:signatureDblpName ?o3 . } }
exists-join-3-chain-1 [EXISTS JOIN chain of three large predicates with the largest sum of join sizes]	PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> PREFIX dblp: <https://dblp.org/rdf/schema#> SELECT (COUNT(*) AS ?count) WHERE { ?a dblp:signaturePublication ?b . ?b rdf:type ?c . FILTER EXISTS { ?c rdfs:subClassOf ?d . } }
exists-join-3-chain-2 [EXISTS JOIN chain of three large predicates with the largest sum of join sizes]	PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> PREFIX dblp: <https://dblp.org/rdf/schema#> SELECT (COUNT(*) AS ?count) WHERE { ?a dblp:signaturePublication ?b . FILTER EXISTS { ?b rdf:type ?c . ?c rdfs:subClassOf ?d . } }
union-no-constraint [UNION of two large predicates, no constraint]	PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> PREFIX dblp: <https://dblp.org/rdf/schema#> SELECT (COUNT(*) AS ?count) WHERE { { ?s rdf:type ?o } UNION { ?s dblp:hasSignature ?o } }
union-constraint-from-star [UNION of two large predicates, constrained by a large predicate known to have join partners]	PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> PREFIX dblp: <https://dblp.org/rdf/schema#> SELECT (COUNT(*) AS ?count) WHERE { { ?s dblp:signatureOrdinal ?o1 } UNION { ?s rdf:type ?o1 } ?s dblp:signatureDblpName ?o2 }
union-constraint-small-join [UNION of two large predicates, constrained by a join with a small result]	PREFIX dblp: <https://dblp.org/rdf/schema#> SELECT (COUNT(*) AS ?count) WHERE { { ?s dblp:publishedAsPartOf ?o1 } UNION { ?s dblp:publishedInStream ?o1 } ?s dblp:publishedInJournalVolume ?o2 }
union-constraint-large-join [UNION of two large predicates, constrained by a join with a large result]	PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> PREFIX dblp: <https://dblp.org/rdf/schema#> SELECT (COUNT(*) AS ?count) WHERE { { ?s dblp:hasSignature ?o1 } UNION { ?s rdf:type ?o1 } ?s dblp:createdBy ?o2 }
union-constraint-filter-restrictive [UNION of two large predicates, constraint by restrictive FILTER]	PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> PREFIX dblp: <https://dblp.org/rdf/schema#> SELECT (COUNT(*) AS ?count) WHERE { { ?s rdf:type ?o } UNION { ?s dblp:hasSignature ?o } FILTER (?s = ?o) }
multicolumn-join-small [Multicolumn JOIN small]	PREFIX dblp: <https://dblp.org/rdf/schema#> SELECT (COUNT(*) AS ?count) WHERE { ?s dblp:subStream ?o . ?s dblp:relatedStream ?o }
multicolumn-join-large [Multicolumn JOIN large]	PREFIX dblp: <https://dblp.org/rdf/schema#> SELECT (COUNT(*) AS ?count) WHERE { ?s dblp:createdBy ?o . ?s dblp:authoredBy ?o }
group-by-count-object-high-multiplicity [GROUP BY with COUNT, for object with high multiplicity]	PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> SELECT ?object (COUNT(?subject) AS ?count) { ?subject rdf:type ?object . } GROUP BY ?object ORDER BY DESC(?count) LIMIT 10
group-by-count-object-low-multiplicity [GROUP BY with COUNT, for object with low multiplicity]	PREFIX dblp: <https://dblp.org/rdf/schema#> SELECT ?object (COUNT(?subject) AS ?count) { ?subject dblp:hasSignature ?object . } GROUP BY ?object ORDER BY DESC(?count) LIMIT 10
group-by-count-object-wrong-sort-order [GROUP BY with COUNT, for object but not sorted by object]	PREFIX dblp: <https://dblp.org/rdf/schema#> SELECT ?o1 (COUNT(?s) AS ?count) { ?s dblp:bibtexType ?o1 . ?s dblp:hasSignature ?o2 . } GROUP BY ?o1 ORDER BY DESC(?count) LIMIT 10
group-by-complex-aggregate [GROUP BY with COUNT and MIN and MAX and SAMPLE]	PREFIX dblp: <https://dblp.org/rdf/schema#> SELECT ?o1 (COUNT(?s) AS ?count) (MIN(?s) AS ?min) (MAX(?s) AS ?max) (SAMPLE(?s) AS ?sample) { ?s dblp:bibtexType ?o1 . ?s dblp:hasSignature ?o2 . } GROUP BY ?o1 ORDER BY DESC(?count) LIMIT 10
group-by-implicit-numeric-baseline [Implicit GROUP BY with COUNT on numeric predicate as baseline]	PREFIX dblp: <https://dblp.org/rdf/schema#> SELECT (COUNT(?o) AS ?count) { ?s dblp:numberOfCreators ?o. }
group-by-implicit-numeric-sum [Implicit GROUP BY with SUM on numeric predicate]	PREFIX dblp: <https://dblp.org/rdf/schema#> SELECT (SUM(?o) AS ?sum) { ?s dblp:numberOfCreators ?o. }
group-by-implicit-numeric-min [Implicit GROUP BY with MIN on numeric predicate]	PREFIX dblp: <https://dblp.org/rdf/schema#> SELECT (MIN(?o) AS ?min) { ?s dblp:numberOfCreators ?o. }
group-by-implicit-numeric-max [Implicit GROUP BY with MAX on numeric predicate]	PREFIX dblp: <https://dblp.org/rdf/schema#> SELECT (MAX(?o) AS ?max) { ?s dblp:numberOfCreators ?o. }
group-by-implicit-numeric-avg [Implicit GROUP BY with AVG on numeric predicate]	PREFIX dblp: <https://dblp.org/rdf/schema#> SELECT (AVG(?o) AS ?avg) { ?s dblp:numberOfCreators ?o. }
group-by-implicit-string-baseline [Implicit GROUP BY with COUNT on string predicate as baseline]	PREFIX dblp: <https://dblp.org/rdf/schema#> SELECT (COUNT(?o) AS ?count) { ?s dblp:signatureDblpName ?o. }
group-by-implicit-string-min [Implicit GROUP BY with MIN on string predicate]	PREFIX dblp: <https://dblp.org/rdf/schema#> SELECT (MIN(?o) AS ?min) { ?s dblp:signatureDblpName ?o. }
group-by-implicit-string-max [Implicit GROUP BY with MAX on string predicate]	PREFIX dblp: <https://dblp.org/rdf/schema#> SELECT (MAX(?o) AS ?max) { ?s dblp:signatureDblpName ?o. }
group-by-string-groupconcat [GROUP BY with GROUP_CONCAT on string predicate with high subject multiplicity]	PREFIX dblp: <https://dblp.org/rdf/schema#> SELECT (SUM(STRLEN(?cat)) AS ?sum) { { SELECT (GROUP_CONCAT(?o; SEPARATOR=" ") AS ?cat) { ?s dblp:signatureDblpName ?o. } GROUP BY ?s } }
distinct-count-object-high-multiplicity [COUNT DISTINCT, for object with high multiplicity]	PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> SELECT (COUNT(DISTINCT ?object) AS ?count) { ?subject rdf:type ?object . }
distinct-count-object-low-multiplicity [COUNT DISTINCT, for object with low multiplicity]	PREFIX dblp: <https://dblp.org/rdf/schema#> SELECT (COUNT(DISTINCT ?object) AS ?count) { ?subject dblp:hasSignature ?object . }
distinct-count-object-wrong-sort-order [COUNT DISTINCT, for object but not sorted by object]	PREFIX dblp: <https://dblp.org/rdf/schema#> SELECT (COUNT(DISTINCT ?o1) AS ?count) { ?s dblp:bibtexType ?o1 . ?s dblp:hasSignature ?o2 . }
transitive-path-plus [Transitive path with plus]	PREFIX dblp: <https://dblp.org/rdf/schema#> SELECT (COUNT(*) AS ?count) { ?s dblp:relatedStream+ ?o }
transitive-path-plus-fixed-subject [Transitive path with plus and a fixed subject]	PREFIX dblp: <https://dblp.org/rdf/schema#> SELECT (COUNT(*) AS ?count) { <https://dblp.org/streams/conf/damp> dblp:relatedStream+ ?o }
transitive-path-large-join-and-plus [Transitive path with a large join and plus]	PREFIX dblp: <https://dblp.org/rdf/schema#> SELECT (COUNT(*) AS ?count) { ?s dblp:publishedInStream/dblp:relatedStream+ ?o }
transitive-path-small-join-and-plus [Transitive path with a small join and plus]	PREFIX dblp: <https://dblp.org/rdf/schema#> SELECT (COUNT(*) AS ?count) { ?s dblp:subStream/dblp:relatedStream+ ?o }
regex-3-contains [CONTAINS filter with fixed string of length 3]	PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> SELECT (COUNT(*) AS ?count) WHERE { ?s rdfs:label ?o FILTER CONTAINS(?o, "com") }
regex-3-fixed [REGEX filter with fixed string of length 3]	PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> SELECT (COUNT(*) AS ?count) WHERE { ?s rdfs:label ?o FILTER REGEX(?o, "com") }
regex-3 [REGEX filter with expression of length 3]	PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> SELECT (COUNT(*) AS ?count) WHERE { ?s rdfs:label ?o FILTER REGEX(?o, "c.m") }
regex-prefix-1 [REGEX filter with prefix of length 1]	PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> SELECT (COUNT(*) AS ?count) WHERE { ?s rdfs:label ?o FILTER REGEX(?o, "^C") }
regex-prefix-2 [REGEX filter with prefix of length 2]	PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> SELECT (COUNT(*) AS ?count) WHERE { ?s rdfs:label ?object FILTER REGEX(?object, "^Co") }
regex-prefix-3 [REGEX filter with prefix of length 3]	PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> SELECT (COUNT(*) AS ?count) WHERE { ?s rdfs:label ?object FILTER REGEX(?object, "^Com") }
strlen [String length for large string predicate]	PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> SELECT (SUM(STRLEN(?o)) AS ?checksum) { ?s rdfs:label ?o . }
strbefore [STRBEFORE string function]	PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> SELECT (SUM(STRLEN(STRBEFORE(?o, "a"))) AS ?checksum) { ?s rdfs:label ?o . }
strafter [STRAFTER string function]	PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> SELECT (SUM(STRLEN(STRAFTER(?o, "a"))) AS ?checksum) { ?s rdfs:label ?o . }
strstarts [STRSTARTS string function]	PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> PREFIX xsd: <http://www.w3.org/2001/XMLSchema#> SELECT (SUM(xsd:integer(STRSTARTS(?o, "a"))) AS ?count) { ?s rdfs:label ?o . }
strends [STRENDS string function]	PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> PREFIX xsd: <http://www.w3.org/2001/XMLSchema#> SELECT (SUM(xsd:integer(STRENDS(?o, "a"))) AS ?count) { ?s rdfs:label ?o . }
result-size-tiny [Export 10 tuples]	PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> SELECT ?s ?o WHERE { ?s rdf:type ?o } LIMIT 10
result-size-small [Export 1000 tuples]	PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> SELECT ?s ?o WHERE { ?s rdf:type ?o } LIMIT 1000
result-size-medium [Export 100K tuples]	PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> SELECT ?s ?o WHERE { ?s rdf:type ?o } LIMIT 100000
result-size-large [Export 1M tuples]	PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> SELECT ?s ?o WHERE { ?s rdf:type ?o } LIMIT 1000000
result-size-xlarge [Export 10M tuples]	PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> SELECT ?s ?o WHERE { ?s rdf:type ?o } LIMIT 10000000
numeric-baseline [Baseline for numeric queries]	PREFIX dblp: <https://dblp.org/rdf/schema#> SELECT (SUM(?o) AS ?sum) WHERE { ?s dblp:numberOfCreators ?o }
numeric-abs [ABS function]	PREFIX dblp: <https://dblp.org/rdf/schema#> SELECT (SUM(ABS(?o)) AS ?sum) WHERE { ?s dblp:numberOfCreators ?o }
numeric-ceil [CEIL function]	PREFIX dblp: <https://dblp.org/rdf/schema#> SELECT (SUM(CEIL(?o)) AS ?sum) WHERE { ?s dblp:numberOfCreators ?o }
numeric-floor [FLOOR function]	PREFIX dblp: <https://dblp.org/rdf/schema#> SELECT (SUM(FLOOR(?o)) AS ?sum) WHERE { ?s dblp:numberOfCreators ?o }
numeric-round [ROUND function]	PREFIX dblp: <https://dblp.org/rdf/schema#> SELECT (SUM(ROUND(?o)) AS ?sum) WHERE { ?s dblp:numberOfCreators ?o }
numeric-add [Addition]	PREFIX dblp: <https://dblp.org/rdf/schema#> SELECT (SUM(?o + ?o) AS ?sum) WHERE { ?s dblp:numberOfCreators ?o }
numeric-greater [Greater than]	PREFIX dblp: <https://dblp.org/rdf/schema#> SELECT (SUM(?o > 0) AS ?sum) WHERE { ?s dblp:numberOfCreators ?o }
numeric-filter-bin-search-fifty-fifty [Numeric FILTER that filters out 50 percent of the values]	PREFIX dblp: <https://dblp.org/rdf/schema#> SELECT (COUNT(?s) AS ?count) { ?s dblp:numberOfCreators ?o . FILTER (?o >= 2) }
numeric-filter-bin-search-seventy-thirty [Numeric FILTER that filters out 70 percent of the values]	PREFIX dblp: <https://dblp.org/rdf/schema#> SELECT (COUNT(?s) AS ?count) { ?s dblp:numberOfCreators ?o . FILTER (?o >= 3) }
numeric-filter-bin-search-ninetyfive-five [Numeric FILTER that filters out 95 percent of the values]	PREFIX dblp: <https://dblp.org/rdf/schema#> SELECT (COUNT(?s) AS ?count) { ?s dblp:numberOfCreators ?o . FILTER (?o >= 7) }
filter-few-results [FILTER that filters out most rows and has few results]	PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> SELECT (COUNT(?s) AS ?count) { ?s rdf:type ?o . FILTER (?s = ?o) }
filter-many-results [FILTER that filters out few rows and has many results]	PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> SELECT (COUNT(?s) AS ?count) { ?s rdf:type ?o . FILTER (?s != ?o) }
filter-language-en [FILTER on string predicate for only English literals]	PREFIX dblp: <https://dblp.org/rdf/schema#> SELECT (COUNT(?s) AS ?count) { ?s dblp:signatureDblpName ?o . FILTER(LANG(?o) = "en") }
date-year [YEAR function]	PREFIX dblp: <https://dblp.org/rdf/schema#> SELECT (SUM(YEAR(?o)) AS ?sum) WHERE { ?s dblp:yearOfPublication ?o }
date-month [MONTH function]	PREFIX dblp: <https://dblp.org/rdf/schema#> SELECT (SUM(MONTH(?o)) AS ?sum) WHERE { ?s dblp:yearOfPublication ?o }
date-day [DAY function]	PREFIX dblp: <https://dblp.org/rdf/schema#> SELECT (SUM(DAY(?o)) AS ?sum) WHERE { ?s dblp:yearOfPublication ?o }
number-of-triples [Total number of triples]	SELECT (COUNT(?s) AS ?count) WHERE { ?s ?p ?o }
number-of-subjects [Total number of distinct subjects]	SELECT (COUNT(DISTINCT ?s) AS ?count) WHERE { ?s ?p ?o }
number-of-predicates [Total number of distinct predicates]	SELECT (COUNT(DISTINCT ?p) AS ?count) WHERE { ?s ?p ?o }
number-of-objects [Total number of distinct objects]	SELECT (COUNT(DISTINCT ?o) AS ?count) WHERE { ?s ?p ?o }
number-of-literals [Total number of literals]	SELECT (COUNT(?o) AS ?count) WHERE { ?s ?p ?o FILTER ISLITERAL(?o) }
number-of-blank-nodes [Total number of blank nodes]	SELECT (COUNT(?s) AS ?count) WHERE { ?s ?p ?o FILTER ISBLANK(?s) }
